import static util.Filez.*
import util.ReplacementMap
import packager.BuildDir


ext.dirs = new BuildDir(buildDir)
ext.releaseBundleBaseName = 'ome-smuggler'
ext.releaseBundleVersion = '0.1.0'
ext.releaseBundleClassifier = 'beta'

def serverJarFile() { toFile(project(':server').jar) }
def cliJarFile() { toFile(project(':cli').jar) }

def configureBaseBundle(AbstractArchiveTask task) {
    task.from dirs.staging.baseDir
    task.into releaseBundleBaseName
    task.includeEmptyDirs = true
    task.destinationDir = dirs.distDir
    task.baseName = releaseBundleBaseName
    task.version = releaseBundleVersion
    task.classifier = releaseBundleClassifier
}

def configureGenericBundle(AbstractArchiveTask task) {
    configureBaseBundle(task)
    task.include '**/*.md', '**/*.sh', '**/*.bat', '**/*.jar'
}


task makeBuildDirs << {
    dirs.subdirs().each { d -> mkdir(d) }
}

task copyReadme(type: Copy, dependsOn: makeBuildDirs) {
    from 'src/README.md'
    into dirs.staging.baseDir
}

task copyLicense(type: Copy, dependsOn: makeBuildDirs) {
    from toFile(rootProject.projectDir, 'LICENSE.md')
    into dirs.staging.baseDir
}

task copyJars(dependsOn: [makeBuildDirs, ':server:bootRepackage',
                          ':cli:bootRepackage']) << {
    copy {
        from serverJarFile()
        from cliJarFile()
        into dirs.staging.libDir
    }
}

task copyScripts(dependsOn: makeBuildDirs) << {
    def m = [ '@lib.dir.name@' : dirs.staging.libDir.name
            , '@jar.file.name@' : serverJarFile().name
            , '@config.dir.name@' : dirs.staging.configDir.name
            , '@data.dir.name@' : dirs.staging.dataDir.name
            , '@log.dir.name@' : dirs.staging.logDir.name
            ] as ReplacementMap
    copy {
        from 'src/generic/'
        include '*.template'
        into dirs.staging.binDir
        rename { it - ~/\.template$/ }
        filter m.&replaceAll
        fileMode 0755
    }
}

task releaseGenericZip(type: Zip) {
    configureGenericBundle(releaseGenericZip)
}

task releaseGenericTar(type: Tar) {
    configureGenericBundle(releaseGenericTar)
    compression = Compression.GZIP
}

task releaseGeneric(dependsOn: [copyReadme, copyLicense, copyJars, copyScripts,
                                releaseGenericTar, releaseGenericZip])

task release (dependsOn: [releaseGeneric]) {
    group 'distribution'
    description 'Releases generic and platform-specific server deployment bundles.'
}

task clean {
    group 'build'
    description 'Deletes the build directory.'
    doLast {
        delete(buildDir)
    }
}
